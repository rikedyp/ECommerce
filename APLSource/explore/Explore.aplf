 Explore
 'order'⎕NS ⍬
 path←'"C:\g\ECommerce\archive\olist_orders_dataset.csv"'~'"'
 order.(data header)←⎕CSV path ⍬ 1 1
 'customer'⎕NS ⍬
 customer.(data header)←(1∘↓,⍥⊂1∘⌷)##.customer
⍝ path←'"C:\g\ECommerce\archive\olist_customers_dataset.csv"'~'"'
⍝ customer.(data header)←⎕CSV path ⍬ 1 1
 'items'⎕NS ⍬
 path←'"C:\g\ECommerce\archive\olist_order_items_dataset.csv"'~'"'
 items.(data header)←⎕CSV path ⍬ 4 1
 'product'⎕NS ⍬
 path←'"C:\g\ECommerce\archive\olist_products_dataset.csv"'~'"'
 product.(data header)←⎕CSV path ⍬ 4 1

 From←{⍵.data[;⍵.header⍳⊆⍺]}
 _In←{⍺⍳⍥(⍺⍺∘From)⍵}

⍝ Customers who made the most orders
 cuid←'customer_unique_id'From customer
 cid←cuid[customer('customer_id'_In)order]
 name←'first_name' 'last_name'From customer
 ord_by_cust←{name[cuid⍳⊣/⍵;],⍵}{⍺,≢⍵}⌸cid
 ⎕←5↑ord_by_cust[⍒⊢/ord_by_cust;]

⍝ Product category popularity by state
 ⎕←10↑ord_state←('customer_state'From customer)[customer('customer_id'_In)order]
 ord_prod←('unknown',⍨⍥⊆'product_id'From items)[items('order_id'_In)order]
 ord_prod_cat←('unknown',⍨⍥⊆'product_category_name'From product)[('product_id'From product)⍳ord_prod]
 ⎕←⍪3↑ord_state{⊂{⍺,≢⍵}⌸⍵}⌸ord_prod_cat
⍝ Sort by most popular
⍝ Sort by most orders in each state
⍝ Check that it matches grouping orders by state?

 ⍝ Possible: multi-table pivot table DSL?
 _U_←{⍵⍵⍣¯1⊢⍺⍺ ⍵⍵ ⍵}
 ⎕←((⊂∘⍒⌷⊢)_U_⌽)¨2↑ord_state{⊂{⍺,≢⍵}⌸⍵}⌸ord_prod_cat
